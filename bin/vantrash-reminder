#!/usr/bin/perl
use strict;
use warnings;
use LWP::UserAgent;
use Getopt::Long;
use JSON::XS 'encode_json';

my $zone;
my $id;
my $name;
my $password;
my $email;
my $offset;
my $delete;
my $server_base = 'http://vantrash.ca';
GetOptions(
    'z|zone=s'     => \$zone,
    'i|id=s'       => \$id,
    'n|name=s'     => \$name,
    'p|password=s' => \$password,
    'e|email=s'    => \$email,
    'o|offset=s'   => \$offset,
    'delete'       => \$delete,
    'server=s'     => \$server_base,
) or usage();

if ($delete) {
    usage() unless $zone and $id;
    delete_reminder();
}
else {
    usage() unless $zone and $name and $email;
    add_reminder();
}
exit;

sub delete_reminder {
    my $uri = "$server_base/zones/$zone/reminders/$id";
    my $ua = LWP::UserAgent->new;
    my $req = HTTP::Request->new('DELETE', $uri);
    my $resp = $ua->request($req);
    print $resp->status_line, "\n";
}

sub add_reminder {
    my $uri = "$server_base/zones/$zone/reminders";
    my $ua = LWP::UserAgent->new;
    my $req = HTTP::Request->new('PUT', $uri);
    $req->content( encode_json( {
                name => $name,
                email => $email,
                ($password ? (password => $password) : ()),
                ($offset   ? (offset   => $offset  ) : ()),
            }
        )
    );

    my $resp = $ua->request($req);
    print $resp->status_line, "\n";
    print $resp->headers->header('Location'), "\n";
}


sub usage {
    die <<EOT
USAGE: $0 <options>

WHERE options are:
  --zone=foo        (mandatory)
  --id=foo          (mandatory)
  --email=foo       (mandatory)
  --password=foo
  --offset=foo
EOT
}
